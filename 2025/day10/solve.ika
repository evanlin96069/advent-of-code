#include "aoc.ika"

var s: Str;
if (!read_file("input.txt", &s)) {
    "Failed to read file\n";
    return 1;
}

struct Machine {
    lights: u32,
    masks: VecU32,
    buttons: Vec, // Vec<VecU32>
    counters: VecU32,
};

var machines: Vec = vec_new(sizeof(Machine));

fn parse_list(s: Str) VecU32 {
    var result: VecU32 = vecu32_new();

    var num_it: SplitIterator = str_split(s, ",");
    var num_str: Str;
    while (str_split_next(&num_it, &num_str)) {
        var num: u32;
        str_parse_u32(num_str, &num);
        vecu32_push(&result, num);
    }

    return result;
}

var line_it: SplitIterator = str_split(s, "\n");
var line: Str;
while (str_split_next(&line_it, &line)) {
    var machine: Machine;

    var i: i32;
    var space_it: SplitIterator = str_split(line, " ");

    // parse lights
    var lights_str: Str;
    str_split_next(&space_it, &lights_str);
    var lights: u32 = 0;
    lights_str = str_sub(lights_str, 1, lights_str.len - 1);
    i = lights_str.len - 1;
    while (i >= 0) : (i -= 1) {
        lights <<= 1;
        if (lights_str.ptr[i] == '#') {
            lights |= 0x1;
        }
    }
    machine.lights = lights;

    // parse buttons
    machine.buttons = vec_new(sizeof(VecU32));
    machine.masks = vecu32_new();
    var num_list_str: Str;
    while (str_split_next(&space_it, &num_list_str) && num_list_str.ptr[0] == '(') {
        num_list_str = str_sub(num_list_str, 1, num_list_str.len - 1);
        var button: VecU32 = parse_list(num_list_str);
        var mask: u32 = 0;
        i = 0;
        while (i < button.len) : (i += 1) {
            mask |= (1 << button.data[i]);
        }
        vec_push(&machine.buttons, &button);
        vecu32_push(&machine.masks, mask);
    }


    // parse counters
    num_list_str = str_sub(num_list_str, 1, num_list_str.len - 1);
    machine.counters = parse_list(num_list_str);
    vec_push(&machines, &machine);
}

fn part1() void {
    var total: u32 = 0;

    var index: u32 = 0;
    while (index < machines.len) : (index += 1) {
        var machine: *Machine = vec_get(&machines, index);
        var best_count: u32 = 0xFFFFFFFF;

        var n: u32 = machine.masks.len;
        var mask: u32 = 1;
        while (mask < (1 << n)) : (mask += 1) {
            var result: u32 = 0;
            var count: u32 = 0;

            var i: u32 = 0;
            while (i < n) : (i += 1) {
                if (((mask >> i) & 1) != 0) {
                    result ^= machine.masks.data[i];
                    count += 1;
                }
            }
            if (result == machine.lights) {
                if (count < best_count) {
                    best_count = count;
                }
            }
        }

        assert(best_count != 0xFFFFFFFF);
        total += best_count;
    }

    "total=%d\n", total;
}

fn part2() void {
    // in Python
}


part1();
part2();
